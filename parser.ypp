%{
	#include "Utilities.h"
	#include "Tables.h"
	#include "Types.h"
	#include "hw3_output.hpp"
	#include "bp.hpp"
%}


%token VOID
%token INT
%token BYTE
%token B
%token BOOL
%token CONST
%token TRUE
%token FALSE
%token RETURN
%token IF
%token WHILE
%token BREAK
%token CONTINUE
%token SC
%token COMMA
%token ID
%token NUM
%token STRING

%right ASSIGN
%left OR
%left AND
%left RELOP_EQUAL
%nonassoc RELOP_COMPARE
%left BINOP_ADD
%left BINOP_MUL
%right NOT
%left LPAREN
%left RPAREN
%left LBRACE
%left RBRACE
%nonassoc ELSE


%%


Program											:	{inside_while_counter = 0; declare_standart_functions(); declare_divion_error_str();}
													Funcs
													{check_main_exist(); close_scope(); code_buffer.printGlobalBuffer(); code_buffer.printCodeBuffer();}
;

Funcs											:	/*epsilon*/
													{if (yychar != YYEOF) yyerror("not EOF");}
												|	FuncDecl
													Funcs
;

FuncDecl										:	RetType
													ID
													LPAREN
													Formals
													RPAREN
													{add_func_to_symbol_table($2->get_name(), $1->get_name(), $4->get_args_types(), $2->get_yylineno()); emit_define_function($1->get_name(), $2->get_name(), $4->get_args_types()); emit_allocate_stack();}
													LBRACE
													{open_scope(); add_func_args_to_symbol_table($4->get_args_types(), $4->get_args_names());}
													Statements
													RBRACE
													{close_scope(); emit_default_return($1->get_name());}
;

RetType											:	Type
													{$$ = new RetTypeCls($1->get_name());}
												|	VOID
													{$$ = new RetTypeCls("VOID");}
;

Formals											:	/*epsilon*/
													{$$ = new FormalsCls();}
												|	FormalsList
													{$$ = new FormalsCls($1->get_args_types(), $1->get_args_names());}
;

FormalsList										:	FormalDecl
													{$$ = new FormalsListCls(); $$->add_new_func_arg($1);}
												|	FormalDecl
													COMMA
													FormalsList
													{$$ = new FormalsListCls($3->get_args_types(),$3->get_args_names()); $$->add_new_func_arg($1); }
;

FormalDecl										:	TypeAnnotation
													Type
													ID
													{$$ = new FormalDeclCls($1->get_is_const(), $2->get_name(), $3->get_name());}
;

Statements										:	Statement
													{$$ = new StatementsCls(STATEMENTS_TO_STATEMENT, $1);}
												|	Statements
													Statement
													{$$ = new StatementsCls(STATEMENTS_TO_STATEMENTS_STATEMENT, $1, $2);}
;

Statement										:	LBRACE
													{open_scope();}
													Statements
													RBRACE
													{close_scope(); $$ = new StatementCls(STATEMENT_TO_STATEMENTS, $3);}
												|	TypeAnnotation
													Type
													ID
													SC
													{check_assign_to_const($1->get_is_const()); add_var_to_symbol_table($1->get_is_const(),$2->get_name(), $3->get_name()); $$ = new StatementCls(STATEMENT_TO_TYPE_ID, $3);}
												|	TypeAnnotation
													Type
													ID
													ASSIGN
													Exp
													SC
													{check_valid_types_for_assign($2->get_name(), $5->get_type()); add_var_to_symbol_table($1->get_is_const(),$2->get_name(), $3->get_name()); $$ = new StatementCls(STATEMENT_TO_TYPE_ID_EXP, $1, $3, $5);}
												|	ID
													ASSIGN
													Exp
													SC
													{check_var_valid_for_assign($1->get_name()); std::string& id_type = get_id_type($1->get_name()); check_valid_types_for_assign(id_type, $3->get_type()); $$ = new StatementCls(STATEMENT_TO_ID_EXP, $1, $3);}
												|	Call
													SC
												|	RETURN
													SC
													{check_valid_ret_type("VOID");}
												|	RETURN
													Exp
													SC
													{assert_not_void($2->get_type()); check_valid_ret_type($2->get_type());}
												|	IF
													{open_scope();}
													LPAREN
													Exp
													{check_matching_types("BOOL", $4->get_type());}
													RPAREN
													M
													Statement
													{close_scope();}
													IfElse
													{$$ = new StatementCls(STATEMETN_TO_IF, $4, $7, $8, $10);}
												|	N
													WHILE
													{inside_while_counter++; open_scope();}
													LPAREN
													M
													Exp
													{check_matching_types("BOOL", $6->get_type());}
													RPAREN
													M
													Statement
													{close_scope(); inside_while_counter--; $$ = new StatementCls(STATEMENT_TO_WHILE, $1, $5, $6, $9, $10);}
												|	BREAK
													SC
													{check_legal_break(); $$ = new StatementCls(STATEMENT_TO_BREAK);}
												|	CONTINUE
													SC
													{check_legal_continue(); $$ = new StatementCls(STATEMENT_TO_CONTINUE);}
;

IfElse											: 	{$$ = new IfElseCls();}
													// we picked up a random token with lower priority compared
													// to ELSE, to solve shift-reduce conflict
													%prec ASSIGN
												| 	ELSE
													{open_scope();}
													N
													M
													Statement
													{close_scope(); $$ = new IfElseCls($3, $4, $5, false);}
;

Call											:	ID
													LPAREN
													ExpList
													RPAREN
													{std::string& func_ret_type = check_valid_func_call($1->get_name(), $3->get_args_types()); $$ = new CallCls(func_ret_type);}
												|	ID
													LPAREN
													RPAREN
													{std::string& func_ret_type = check_valid_func_call($1->get_name()); $$ = new CallCls(func_ret_type);}
;

ExpList											:	Exp
													{$$ = new ExpListCls(); $$->add_new_func_arg($1);}
												|	Exp
													COMMA
													ExpList
													{$$ = new ExpListCls($3->get_args_types()); $$->add_new_func_arg($1);}
;

Type											:	INT
													{$$ = new TypeCls("INT");}
												|	BYTE
													{$$ = new TypeCls("BYTE");}
												|	BOOL
													{$$ = new TypeCls("BOOL");}
;

TypeAnnotation									:	/*epsilon*/
													{$$ = new TypeAnnotationCls(false);}
												|	CONST
													{$$ = new TypeAnnotationCls(true);}
;

Exp												:	LPAREN
													Exp
													RPAREN
													{$$ = new ExpCls($2->get_type(), $2->get_value(), REGULAR, EXP_TO_LPAREN_EXP_RPAREN, $2);}
												|	Exp
													BINOP_MUL
													Exp
													{$$ = new ExpCls(handle_binop_exp($1->get_type(), $3->get_type()), std::string("0"), REGULAR, EXP_TO_EXP_BINOP_MUL_EXP, $1, $3, $2);}
												|	Exp
													BINOP_ADD
													Exp
													{$$ = new ExpCls(handle_binop_exp($1->get_type(), $3->get_type()), std::string("0"), REGULAR, EXP_TO_EXP_BINOP_ADD_EXP, $1, $3, $2);}
												|	ID
													{$$ = new ExpCls(get_id_type($1->get_name()), std::string("0"), REGULAR, EXP_TO_ID, $1);}
												|	Call
													{$$ = new ExpCls($1->get_type());}					// TODO - we are afraid
												|	NUM
													{$$ = new ExpCls("INT", $1->get_value(), SIMPLE_NUM, EXP_TO_NUM);}
												|	NUM
													B
													{check_byte_range($1->get_value()); $$ = new ExpCls("BYTE", $1->get_value(), SIMPLE_NUM, EXP_TO_NUM_B);}
												|	STRING
													{$$ = new ExpCls("STRING", $1->get_value(), REGULAR, EXP_TO_STRING, $1);}
												|	TRUE
													{$$ = new ExpCls("BOOL", std::string("1"), SIMPLE_NUM, EXP_TO_TRUE);}
												|	FALSE
													{$$ = new ExpCls("BOOL", std::string("0"), SIMPLE_NUM, EXP_TO_FALSE);}
												|	NOT
													Exp
													{check_matching_types("BOOL", $2->get_type()); $$ = new ExpCls("BOOL", std::string("0"), REGULAR, EXP_TO_NOT_EXP, $2);}
												|	Exp
													AND
													M
													Exp
													{check_matching_types("BOOL", $1->get_type()); check_matching_types("BOOL", $4->get_type()); $$ = new ExpCls("BOOL", std::string("0"), REGULAR, EXP_TO_EXP_AND_EXP, $1, $4, $3);}
												|	Exp
													OR
													M
													Exp
													{check_matching_types("BOOL", $1->get_type()); check_matching_types("BOOL", $4->get_type()); $$ = new ExpCls("BOOL", std::string("0"), REGULAR, EXP_TO_EXP_OR_EXP, $1, $4, $3);}
												|	Exp
													RELOP_COMPARE
													Exp
													{handle_relop_exp($1->get_type(), $3->get_type()); $$ = new ExpCls("BOOL", std::string("0"), REGULAR, EXP_TO_EXP_RELOP_COMPARE_EXP, $1, $3, $2);}
												|	Exp
													RELOP_EQUAL
													Exp
													{handle_relop_exp($1->get_type(), $3->get_type()); $$ = new ExpCls("BOOL", std::string("0"), REGULAR, EXP_TO_EXP_RELOP_EQUAL_EXP, $1, $3, $2);}
												|	LPAREN
													Type
													RPAREN
													Exp
													{handle_cast_exp($2->get_name(), $4->get_type(), $4->get_value()); $$ = new ExpCls($2->get_name(), std::string("0"), REGULAR, EXP_TO_CAST, $4);}
;

M												:	{$$ = new MCls();}


N												:	{$$ = new NCls();}

%%


void yyerror(const char* input) {
	output::errorSyn(yylineno);
	exit(1);
}


int main() {
	return yyparse();
}
